initialize() {
	defineConstant("L", 1e8);   // chromosome length
	initializeTreeSeq();
	initializeMutationRate(0);
	initializeMutationType("m1", 0.5, "f", 0.1);   // beneficial
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}
1 late() {
	sim.addSubpop("p1", 500);
	sim.addSubpop("p2", 500);
	
	// remember the root individuals in the tree sequences forever
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals);
	
	// p1 and p2 are each fixed for one beneficial mutation
	p1.genomes.addNewDrawnMutation(m1, asInteger(L * 0.2));
	p2.genomes.addNewDrawnMutation(m1, asInteger(L * 0.8));
	
	// make p3 be an admixture of p1 and p2 in the next generation
	sim.addSubpop("p3", 1000);
	p3.setMigrationRates(c(p1, p2), c(0.5, 0.5));
}
2 late() {
	// get rid of p1 and p2
	p3.setMigrationRates(c(p1, p2), c(0.0, 0.0));
	p1.setSubpopulationSize(0);
	p2.setSubpopulationSize(0);
}
2: late() {
	// output and terminate when both beneficial mutations have fixed
	if (sim.mutationsOfType(m1).size() == 0)
	{
		sim.treeSeqOutput("./ex3_TS.trees");
		sim.simulationFinished();
	}
}
10000 late() {
	stop("Did not reach fixation of beneficial alleles.");
}
